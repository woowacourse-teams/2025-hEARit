// 1. 플러그인 버전 관리를 위한 buildscript 추가
buildscript {
    ext {
        restdocsApiSpecVersion = '0.18.2'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'com.onair'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'jakarta.validation:jakarta.validation-api'

    // Test
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner:4.2.1'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // REST Docs
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'

    // restdocs-api-spec
    testImplementation "com.epages:restdocs-api-spec-restassured:${restdocsApiSpecVersion}"
    testImplementation "com.epages:restdocs-api-spec:${restdocsApiSpecVersion}"

    // Swagger UI
    swaggerUI 'org.webjars:swagger-ui:5.17.14'

    // DB
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    // Lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    // Spring Security + Jwt
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
}

// 4. 생성될 OpenAPI 스펙 파일의 위치 지정
swaggerSources {
    sample {
        setInputFile(file("${project.buildDir}/api-spec/openapi3.yaml"))
    }
}

openapi3 {
    servers = [
            { url = "http://localhost"; description = "Local Server" }
    ]
    title = "Hearit API 문서"
    description = "Spring REST Docs와 Swagger UI를 이용한 API 명세서"
    version = project.version
    format = "yaml"
}


tasks.named('test') {
    useJUnitPlatform()
}


// 2. GenerateSwaggerUI 태스크가 openapi3 태스크 이후에 실행되도록 설정
tasks.withType(org.hidetake.gradle.swagger.generator.GenerateSwaggerUI).configureEach {
    dependsOn 'openapi3' // 'openapi3'가 먼저 실행되어야 함을 명시
}


// 7. bootJar 빌드 시, 생성된 Swagger UI를 static/swagger-ui에 포함
tasks.named('bootJar') {
    dependsOn 'generateSwaggerUISample' // Swagger UI 생성이 먼저 완료되어야 함
    from("${tasks.generateSwaggerUISample.outputDir}") {
        into 'static/swagger-ui'
    }
}
